calculation clamp(x)
  if x > 100 then store 100 in x endif
  if x < 0 then store 0 in x endif
  returns x
endcalc

path line(len)
  store @x - @y in dist
  while dist < 0 do
    add 10 to dist
  done
  while dist > 10 do
    sub 10 from dist
  done
  store clamp(|dist - 5| * 60 - 100) in @red
  store clamp(100 - |dist * 30 - 100|) in @green
  store clamp(100 - |dist * 30 - 200|) in @blue
  walk len
endpath

path recurse(len, up)
  if len < @minlen then
    path line(len)
  else
    store len / sqrt(2) in newlen
    if up > 0 then
      turn left 45
      path recurse(newlen, up)
      turn right 90
      path recurse(newlen, -up)
      turn left 45
    else
      turn right 45
      path recurse(newlen, -up)
      turn left 90
      path recurse(newlen, up)
      turn right 45
    endif
  endif
endpath

begin
  store 0.2 in @minlen
  jump back 10
  turn right 90
  jump 4
  turn left 90
  path recurse(24, 1)
  stop
end